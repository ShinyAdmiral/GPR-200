//------------------------------------------------------------
// TYPE ALIASES & UTILITY FUNCTIONS

// sScalar: alias for a 1D scalar (non-vector)
#define sScalar float

// sCoord: alias for a 2D coordinate
#define sCoord vec2

// sDCoord: alias for a 2D displacement or measurement
#define sDCoord vec2

// sBasis: alias for a 3D basis vector
#define sBasis vec3

// sPoint: alias for a point/coordinate/location in space
#define sPoint vec4

// sVector: alias for a vector/displacement/change in space
#define sVector vec4

//definition of pi
#define pi 3.1415926535897932384626

// color3: alias for a 3D vector representing RGB color
// 	(this is non-spatial so neither a point nor vector)
#define color3 vec3

// color4: alias for RGBA color, which is non-spatial
// 	(this is non-spatial so neither a point nor vector)
#define color4 vec4

// asPoint: promote a 3D vector into a 4D vector 
//	representing a point in space (w=1)
//    v: input 3D vector to be converted
sPoint asPoint(in sBasis v)
{
    return sPoint(v, 1.0);
}

// asVector: promote a 3D vector into a 4D vector 
//	representing a vector through space (w=0)
//    v: input 3D vector to be converted
sVector asVector(in sBasis v)
{
    return sVector(v, 0.0);
}


// lengthSq: calculate the squared length of a vector type
//    x: input whose squared length to calculate
sScalar lengthSq(sScalar x)
{
    return (x * x);
    //return dot(x, x); // for consistency with others
}

sScalar lengthSq(sDCoord x)
{
    return dot(x, x);
}

sScalar lengthSq(sBasis x)
{
    return dot(x, x);
}

sScalar lengthSq(sVector x)
{
    return dot(x, x);
}

//------------------------------------------------------------
// VIEWPORT INFO
//------------------------------------------------------------

// sViewport: info about viewport
struct sViewport{
    sPoint viewportPoint;	// location on the viewing plane 
							// x = horizontal position
							// y = vertical position
							// z = plane depth (negative focal length)
    
	sCoord pixelCoord;		// position of pixel in image
							// x = [0, width)	-> [left, right)
							// y = [0, height)	-> [bottom, top)
    
	sDCoord resolution;		// resolution of viewport
							// x = image width in pixels
							// y = image height in pixels
    
	sDCoord resolutionInv;	// resolution reciprocal
							// x = reciprocal of image width
							// y = reciprocal of image height
    
	sDCoord size;			// in-scene dimensions of viewport
							// x = viewport width in scene units
							// y = viewport height in scene units
    
	sCoord ndc;				// normalized device coordinate
							// x = [-1, +1) -> [left, right)
							// y = [-1, +1) -> [bottom, top)
    
	sCoord uv;				// screen-space (UV) coordinate
							// x = [0, 1) -> [left, right)
							// y = [0, 1) -> [bottom, top)
        
	sScalar aspectRatio;	// aspect ratio of viewport
    
	sScalar focalLength;	// distance to viewing plane
};
    
// initViewport: calculate the viewing plane (viewport) coordinate
//    vp: 		      output viewport info structure
//    viewportHeight: input height of viewing plane
//    focalLength:    input distance between viewer and viewing plane
//    fragCoord:      input coordinate of current fragment (in pixels)
//    resolution:     input resolution of screen (in pixels)
void initViewport(out sViewport vp,
                  in sScalar viewportHeight, in sScalar focalLength,
                  in sCoord fragCoord, in sDCoord resolution)
{
    vp.pixelCoord = fragCoord;
    vp.resolution = resolution;
    vp.resolutionInv = 1.0 / vp.resolution;
    vp.aspectRatio = vp.resolution.x * vp.resolutionInv.y;
    vp.focalLength = focalLength;
    vp.uv = vp.pixelCoord * vp.resolutionInv;
    vp.ndc = vp.uv * 2.0 - 1.0;
    vp.size = sDCoord(vp.aspectRatio, 1.0) * viewportHeight;
    vp.viewportPoint = asPoint(sBasis(vp.ndc * vp.size * 0.5, -vp.focalLength));
}

//------------------------------------------------------------
// RAY INFO
//------------------------------------------------------------

// sRay: info about the ray shot from camera
struct sRay
{
    sPoint origin;		//origin point in scene
    
    sVector direction;	//direction vector in scene
};

// initRayPersp: initialize perspective ray
//    ray: 		   output ray
//    eyePosition: position of viewer in scene
//    viewport:    input viewing plane offset
void initRayPersp(out sRay ray,
             	  in sBasis eyePosition, in sBasis viewport)
{
    // ray origin relative to viewer is the origin
    // w = 1 because it represents a point; can ignore when using
    ray.origin = asPoint(eyePosition);

    // ray direction relative to origin is based on viewing plane coordinate
    // w = 0 because it represents a direction; can ignore when using
    ray.direction = asVector(viewport - eyePosition);                        
}

vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) 
{
    vec2 xy = fragCoord - size / 2.0;
    float z = size.y / tan(radians(fieldOfView) / 2.0);
    return normalize(vec3(xy, -z));
}

//------------------------------------------------------------
// Geometry FUNCTIONS
//------------------------------------------------------------

// sSphere: infor about spheres
struct sSphere{
	vec4 center;  // center point of sphere
    float radius; // radius of sphere
};

// initViewport: Set values for sphere
//    obj: 		    output sphere
//    center: 		input position of sphere
//    radius: 		input radius of sphere
void initSphere (out sSphere obj, in vec3 center, in float radius)
{
    // initialize sphere variables
	obj.center = asPoint(center);
    obj.radius = radius;
}

float mandelbulb(vec3 pos, float iTime) {
    float Power = 3.0 + 5.0;
	float ThetaShift = iTime * .2;
	float PhiShift = iTime * .2;
    
    if(length(pos) > 1.5) return length(pos) - 1.2;
    vec3 z = pos;
    float dr = 1.0, r = 0.0, theta, phi;
    for (int i = 0; i < 6; i++) {
        r = length(z);
        if (r>1.5) break;
        dr =  pow( r, Power-1.0)*Power*dr + 1.0;
        theta = acos(z.z/r) * Power + ThetaShift;
        phi = atan(z.y,z.x) * Power + PhiShift;
        float sinTheta = sin(theta);
        z = pow(r,Power) * vec3(sinTheta*cos(phi), sinTheta*sin(phi), cos(theta)) + pos;
    }
    return 0.5*log(r)*r/dr;
}

float sphereSDF(vec3 p, float r) 
{
    return length(p) - r;
}

float sdBox( vec3 p, vec3 b )
{
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}

float sdTorus(vec3 pos, vec2 t)
{
	vec2 q = vec2(length(pos.xz) - t.x, pos.y);
    return length(q) - t.y;
}

float sdPlane(vec3 pos, vec3 normals, float h)
{
	return dot(pos, normals) + h;
}

float sdCone( in vec3 p, in vec2 c, float h )
{
  // c is the sin/cos of the angle, h is height
  // Alternatively pass q instead of (c,h),
  // which is the point at the base in 2D
  vec2 q = h*vec2(c.x/c.y,-1.0);
    
  vec2 w = vec2( length(p.xz), p.y );
  vec2 a = w - q*clamp( dot(w,q)/dot(q,q), 0.0, 1.0 );
  vec2 b = w - q*vec2( clamp( w.x/q.x, 0.0, 1.0 ), 1.0 );
  float k = sign( q.y );
  float d = min(dot( a, a ),dot(b, b));
  float s = max( k*(w.x*q.y-w.y*q.x),k*(w.y-q.y)  );
  return sqrt(d)*sign(s);
}

float sdoctahedron(vec3 pos, float size)
{
	pos = abs(pos);
    float m = pos.x + pos.y + pos.z - size;
    vec3 q;
    
    if 		(3.0 * pos.x < m) q = pos.xyz;
    else if (3.0 * pos.y < m) q = pos.yzx;
    else if (3.0 * pos.z < m) q = pos.zxy;
    else 	return m * 0.57735027;
        
    float k = clamp(0.5 * (q.z - q.y + size), 0.0, size);
    return length(vec3(q.x, q.y - size + k, q.z-k));
}

float intersectSDF(float distA, float distB) {
    return max(distA, distB);
}

float unionSDF(float distA, float distB) {
    return min(distA, distB);
}

float differenceSDF(float distA, float distB) {
    return max(distA, -distB);
}


//------------------------------------------------------------
// RENDERING FUNCTIONS
//------------------------------------------------------------

// rotateCamera: take in radians and rotate camera
//    ray: 		    ray that we are manipulating
//    rad: 			radial input on a 2D grid
mat3 rotate(vec2 rad){
    //grab trig of mouse input y
    float cosNumY = cos(-rad.y + pi);
    float sinNumY = sin(-rad.y + pi);
    
    //grab trig of mouse input x
    float cosNumX = cos(-rad.x + pi);
    float sinNumX = sin(-rad.x + pi);
    
    //get roptation matrix
    mat3 rm = mat3(   cosNumX, sinNumX * sinNumY,    sinNumX * cosNumY,
            	         	0, 	         cosNumY,         	  -sinNumY, 
                     -sinNumX, cosNumX * sinNumY,    cosNumY * cosNumX);
    
    
    //return rotation
   	return rm;
}

struct FPointLight
{
 	vec3 center;
    vec3 color;
    float intensity;
};
  
void initPointLight(out FPointLight pLight, vec3 center, vec3 color, float intensity)
{
    pLight.center = center;
    pLight.color = color;
    pLight.intensity = intensity;    
}
    

sScalar diffuseIntensity(sBasis surfacePosition, sBasis surfaceNormal, sBasis lightPos, sScalar lightInten)
{
 	sScalar kD; // diffuse coefficient
    sScalar iL; // Attenuation    
	sBasis L = normalize(lightPos - surfacePosition); // light position
    
    kD = dot(surfaceNormal, L); // check how aligned the light and normal are
    kD = max(0., kD);
	sScalar d = length(lightPos - surfacePosition);
    
    iL = 1. / (1. + (d / lightInten) + ( (d * d) / (lightInten * lightInten) ));   
    return kD * iL;
}

float phongReflectance(vec3 pos, vec3 norm, FPointLight pLight, vec3 rayOrigin)
{
   // Phong Reflectance
   vec3 L = normalize(vec3(pLight.center) - pos); // Light Vector        
   vec3 V = normalize(rayOrigin - pos); // View Vector
   vec3 R = reflect(-L, norm);
   float kS = dot(V,R);   
   kS = max(kS, 0.);
    
   float iS = pow(kS, 16.);
   return iS;
}

// MATHS
//https://www.brainvoyager.com/bv/doc/UsersGuide/CoordsAndTransforms/SpatialTransformationMatrices.html
mat4 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);

    return mat4(
        vec4(c, 0, s, 0),
        vec4(0, 1, 0, 0),
        vec4(-s, 0, c, 0),
        vec4(0, 0, 0, 1)
    );
}

mat4 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);

    return mat4(
        vec4(1., 0, 0., 0),
        vec4(0, c, s, 0),
        vec4(0., -s, c, 0),
        vec4(0, 0, 0, 1.)
    );
}

mat4 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);

    return mat4(
        vec4(c, -s, 0., 0),
        vec4(s, c, 0., 0),
        vec4(0., 0, 1, 0),
        vec4(0, 0, 0, 1.)
    );
}